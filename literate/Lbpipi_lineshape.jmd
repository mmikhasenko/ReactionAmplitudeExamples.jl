---
title: $\Lambda_b$(2S) lineshape
author: Misha Mikhasenko
date: 9th September 2019
options:
    dpi : 200
    fig_env : "figure"
    fig_ext : ".pdf"
    out_width : "0.9\\textwidth"
---

```julia; results="hidden"; echo=false
import PyCall
import PyPlot: matplotlib
PyCall.PyDict(matplotlib["rcParams"])["font.family"] = "serif:bold"
# PyCall.PyDict(matplotlib["rcParams"])["font.weight"] = "bold"
PyCall.PyDict(matplotlib["rcParams"])["font.serif"] = "Helvetica" #"STIX"
PyCall.PyDict(matplotlib["rcParams"])["mathtext.fontset"] = "dejavusans"#"Helvetica"
PyCall.PyDict(matplotlib["rcParams"])["text.usetex"] = true
```

# Introduction
The first radial excitation of the $\Lambda_b$, $\Lambda_b(2S)$ is seen in
the spectrum of $\Lambda_b\pi\pi$.
It is a rather broad, therefore, description of the resonance lineshape require proper account for the energy-dependent width. The later is constrained by the three-body unitarity applied to scattering in the $\Lambda_b\pi\pi$ system.
Derivation of the three-body unitarity condition has been done for three-identical scalar particle in [Three-body scattering: Ladders and Resonances (MM et al.)](http://inspirehep.net/record/1731591). This note shows the generalization
of the approach for the case of the baryonic system $\Lambda_b^0\pi^+\pi^-$.

The setup for the decay chains:
 * Overall $J^{P} = 1/2^{+}$ aiming to study $\Lambda_b(2S)$ dynamics
 * intermediate baryon resonances are $\Sigma_b(\frac{1}{2}^+)$ and $\Sigma_b^{* }(\frac{3}{2}^+)$
 * Three decay channels are used:
   - channel $\Lambda_b f_0(500)\,S$-wave with $f_0\to \pi\pi\,S$-wave (NR background)
   - channel $\Sigma_b \pi\,P$-wave with $\Sigma_b\to \Lambda_b\pi\,P$-wave
   - channel $\Sigma_b^* \pi\,P$-wave with $\Sigma_b^* \to \Lambda_b\pi\,P$-wave$
 * Isospin index is omitted as can be absorbed to normalization

Simplifications that can be made due to narrow $\Sigma^{(* )}$ width and absent of overlap
<!-- Assumptions on the dynamics due to narrow width of : -->
 * interferences $\Sigma_b^{( * )}$ is negligible,
 * rescattering between waves is negligible.
These assumptions are justified in section 3.2 and 3.3.

```julia; results = "hidden";
const mΛb = 5.61960
const mπ = 0.14
# intermediate resonances
const mΣb = 5.81065; const ΓΣb = 0.005
const mΣb_x = 5.83032; const ΓΣb_x = 0.0094
#
const mΛb2S = 6.05 # just the peak mass
#
using ThreeBodyDecay
const tbs = ThreeBodySystem(mΛb2S,mπ,mΛb,mπ,
    two_jps = ([0, 1, 0, 1], ['-','+','-','+']))  # 0- 1/2+ 0- 1/2+
```

# Three-particle-state decomposition
The three-particle state is defined with explicit pion permutation symmetry.
$$
  \left| p_1 \lambda, p_2, p_3 \right\rangle = \frac{1}{2}\left(
    | p_1\lambda ; p_2 ; p_3 \rangle
+    | p_1\lambda ; p_3 ; p_2 \rangle
  \right).
$$
with a usual normalization of the single particle state,
$$
\left\langle p'\lambda'|p\lambda\right\rangle = 2E_p\,\delta^3(\vec p\,'-\vec p) \delta_{\lambda'\lambda}.
$$

The state is expanded in partial waves:
$$
\begin{align*}
| p_1\lambda ; p_2 ; p_3 \rangle
&=\sum_{JMLS}\left|P,JMLS\right\rangle_{k}
  \sum_{\nu} D_{M\nu}^{J}(\alpha,\beta,\gamma)
  (Z^{(k)})^{\nu}_{\lambda}(\{\sigma_s\})
\end{align*}
$$
where $|P,JMLS\rangle_{k}$ for $k = 1,2,3$ are partial-wave-projected
states in the isobar basis of the decays $0\to (ij)k$, $(ij)\to i,j$ for $(ijk)\in\{(123),(231),(312)\}$, $\alpha$, $\beta$, and $\gamma$ are
the Euler angles of the three-particle orientation referenced by a direction of one of the final-state particle (particle $1$ by convension).
$Z^{(i)}$ are the angular decay function described in
[Dalitz-Plot decomposition (MM et al.)](http://inspirehep.net/record/1758460).

## Decay amplitude of $\Lambda_b(\frac{1}{2}^+) \to \pi^{\mu_1} (0^-)\,\Lambda_b(\frac{1}{2}^+)\,\pi^{\mu_3} (0^-)\,$
The considered final state contain charge pions only, however, all charge combinations contribute to $\Lambda_b(2S)$ decay, therefore,
the following expressions require separation of pion isospin. Pion isospin projections are denoted by $\mu_i$ with $i=1,3$.

#### Decay amplitudes via $\Sigma_b$
$$
\begin{align}
\left(A^{b}\right)_{\nu\lambda}^{\mu_1,\mu_3} &= H^{b}_{\nu;\mu_3,\mu_1}\frac{d^{1/2}_{\nu,\lambda}(\theta_{23})}{m_{b}^2 - \sigma_1 - i m_{b} \Gamma_{b}(\sigma_1)}V^{b}_{\lambda;\mu_3}\\ \nonumber
            & \qquad + \sum_{\tau=-1/2}^{1/2} \sum_{\lambda'=1/2}^{1/2} H^{b}_{\nu;\mu_1,\mu_3} \frac{d^{1/2}_{\nu,\tau}(\hat{\theta}_{3(1)}) d^{1/2}_{\tau,-\lambda'}(\theta_{12}) d^{1/2}_{\lambda',\lambda}(\zeta^{2}_{3(1)})}{m_{b}^2 - \sigma_3 - i m_{b} \Gamma_{b}(\sigma_3)} V^{b}_{\lambda';\mu_1}
\end{align}
$$
where the angles in term of invariant variables are given in the Appendix A of the paper [Dalitz-Plot decomposition (MM et al.)](http://inspirehep.net/record/1758460)
the couplings are
$$
\begin{align}
  H^{b}_{\nu;\mu,\mu'} &= \langle 1,0;\frac{1}{2},\nu | \frac{1}{2},\nu \rangle \frac{p_i(\sigma_i)}{\sqrt{1+R^2 p_i^2}} \langle 1,\mu;1,\mu' | 0,0 \rangle,\\
  V^{b}_{\lambda';\mu} &= \langle 1,0;\frac{1}{2},\lambda' | \frac{1}{2},\lambda' \rangle \frac{q_i(\sigma_i)}{\sqrt{1+R^2 q_i^2}} \langle 0,0;1,\mu | 1,\mu \rangle,
\end{align}
$$
with $p_i = \lambda^{1/2}(s,\sigma_i,m_\pi^2)/(2\sqrt{s})$, and $q_i = \lambda^{1/2}(\sigma_i,m_{\Lambda_b}^2, m_\pi^2)/(2\sqrt{\sigma_i})$, $i=1,3$.

Here is the implementation:
```julia; results="hidden";
function Σb_lineshape(s,σ)
    p = sqrt(λ(s,σ,mπ^2)/(4s))
    q = sqrt(λ(σ,mΛb^2,mπ^2)/(4σ))
    # P-wave and P-wave
    R = 5.0 # GeV^{-1}
    return p/sqrt(1+R^2*p^2) * q/sqrt(1+R^2*q^2) * BW(σ, mΣb, ΓΣb)
end
#
dc_Σb1 = decay_chain(1, Σb_lineshape; tbs=tbs, two_s = 1, parity='+') # JP = 1/2+
dc_Σb3 = decay_chain(3, Σb_lineshape; tbs=tbs, two_s = 1, parity='+') # JP = 1/2+
full_AΣb(σs,two_λs) = amplitude(σs, two_λs, dc_Σb1) +
                      amplitude(σs, two_λs, dc_Σb3)
```

#### Decay amplitudes via $\Sigma_b^{*}$
$$
\begin{align}
\left(A^{b*}\right)_{\nu\lambda}^{\mu_1,\mu_3} &= H^{b*}_{\nu;\mu_3,\mu_1}\frac{d^{1/2}_{\nu,\lambda}(\theta_{23})}{m_{b*}^2 - \sigma_1 - i m_{b*} \Gamma_{b*}(\sigma_1)}V^{b*}_{\lambda;\mu_3}\\ \nonumber
            & \qquad + \sum_{\tau=-3/2}^{3/2} \sum_{\lambda'=1/2}^{1/2} H^{b*}_{\nu;\mu_1,\mu_3} \frac{d^{1/2}_{\nu,\tau}(\hat{\theta}_{3(1)}) d^{1/2}_{\tau,-\lambda'}(\theta_{12}) d^{1/2}_{\lambda',\lambda}(\zeta^{2}_{3(1)})}{m_{b*}^2 - \sigma_3 - i m_{b*} \Gamma_{b*}(\sigma_3)} V^{b*}_{\lambda';\mu_1}
\end{align}
$$
where the expressions for angles can be found in [Dalitz-Plot decomposition (MM et al.)](http://inspirehep.net/record/1758460),
the couplings are
$$
\begin{align}
  H^{b*}_{\nu;\mu,\mu'} &= \langle 1,0;\frac{3}{2},\nu | \frac{1}{2},\nu \rangle \frac{p_i(\sigma_i)}{\sqrt{1+R^2 p_i^2}} \langle 1,\mu; 1,\mu' | 0,0 \rangle,\\
  V^{b*}_{\lambda';\mu} &= \langle 1,0;\frac{1}{2},\lambda' | \frac{3}{2},\lambda' \rangle \frac{q_i(\sigma_i)}{\sqrt{1+R^2 q_i^2}} \langle 0,0;1,\mu | 1,\mu \rangle.
\end{align}
$$

A peace of julia code:
```julia; results="hidden";
function Σb_x_lineshape(s,σ)
    p = sqrt(λ(s,σ,mπ^2)/(4s))
    q = sqrt(λ(σ,mΛb^2,mπ^2)/(4σ))
    # P-wave and P-wave
    R = 5.0 # GeV^{-1}
    return p/sqrt(1+R^2*p^2) * q/sqrt(1+R^2*q^2) * BW(σ, mΣb_x, ΓΣb_x)
end
#
dc_Σb_x1 = decay_chain(1, Σb_x_lineshape; tbs=tbs, two_s = 3, parity='+') # JP = 3/2+
dc_Σb_x3 = decay_chain(3, Σb_x_lineshape; tbs=tbs, two_s = 3, parity='+') # JP = 3/2+
full_AΣb_x(σs,two_λs) = amplitude(σs, two_λs, dc_Σb_x1) +
                        amplitude(σs, two_λs, dc_Σb_x3)
```

#### Decay amplitude via $f_0$
$$
\begin{align}
\left(A^{0}\right)_{\nu\lambda}^{\mu_1,\mu_3} &= \sum_{\lambda'=-1/2}^{1/2} d^{1/2}_{\nu,-\lambda'}(\hat{\theta}_{2(1)}) d^{1/2}_{\lambda',\lambda}(\zeta^{2}_{2(1)}) \langle 1,\mu_3;1,\mu_1 | 0,0 \rangle.
\end{align}
$$

The lineshape function is constant, $S$-wave everywhere.
```julia; results="hidden";
dc_f0 = decay_chain(2, (s,σ) -> 1.0; tbs=tbs, two_s = 0, parity='+')
full_Af0(σs,two_λs) = amplitude(σs, two_λs, dc_f0)
```

# Unitarity constraint

Unitarity equation reads:
$$
\newcommand{\diff}{{\mathrm{d}}}
\begin{align}
\left\langle p_1'\lambda', p_2', p_3' \right| T - T^\dagger
\left| p_1\lambda, p_2, p_3 \right\rangle &=
i \sum_{\lambda''} \int \tilde{\diff} p_1'' \tilde{\diff} p_2'' \tilde{\diff} p_3''\,
\left\langle p_1'\lambda', p_2', p_3' \right| T^\dagger
\left| p_1''\lambda'', p_2'', p_3'' \right\rangle
\\ &\hspace{2cm}  \nonumber
\times\left\langle p_1''\lambda'', p_2'', p_3'' \right|
T
\left| p_1\lambda, p_2 p_3 \right\rangle
\end{align}
$$
where operator equation $T-T^\dagger = i T^\dagger T$ is sandwiched between
initial and final three-body states, and identity operator is inserted.
$$
\textit{(a part of derivation is dropped)}
$$

With the partial-wave decomposition, it becomes matrix equation for in the space of decay channels $T_{c'c}(\sigma',s,\sigma)$,
with $c$ being collective index for the channel decay channel characterizing $J$, $L$, and $S$.
$$
\textit{(a part of derivation is dropped)}
$$

The amputated scattering amplitude is defined
$$
T_{c'c} = f_{S'}(\sigma')h_{L'}(s,\sigma')
\,\,
\hat{T}_{i'i}(s)
\,\,
f_{S}(\sigma)h_{L}(s,\sigma),
$$
with $f_S(\sigma)$ being the lineshape of the isobar in $c$, and $h_L(s,\sigma)$ including the isobar-spectator threshold and barrier factor.
The fact that the amputated amplitude $\hat{T}$ does not depend on the subchannel energy
reflects the factorization assumption.

Unitarity equation reads,
$$
\begin{equation}
\hat{T}_{c'c} - \hat{T}_{c'c}^\dagger =
i \hat{T}_{c'c''}^\dagger \Phi_{c''c'''} \hat{T}_{c'''c},
\end{equation}
$$
there $\Phi_{c'c}$ is the matrix of decay-chain overlaps
$$
\begin{equation}
\Phi =
\begin{pmatrix}
  \rho_{0} & z & z^* \\
  z & \rho(1+y)  & x \\
  z^* & x & \rho_{* }(1+y^* )
\end{pmatrix}
\end{equation}
$$
where $\rho_{f}$, $\rho$, and $\rho_{* }$ are the quasi-two-body phase space for
the decay chains $f_0\Lambda_b\,S$-wave, $\Sigma_b\pi\,P$-wave and $\Sigma_b^* \pi\,P$
respectively; $x$ is a small quantity given by an overlap
of $\Sigma_b$ and $\Sigma_b^* $ bands on the Dalitz plot,
$y^{(* )}$ is introduced as a relative contribution
of the $\Sigma_b^{(* )}$ bands with same resonance in the cross channel.
a variable $z^{(* )}$ stand for the overlap between $\Lambda_b f_0\,S$-wave
decay amplitude and $\Sigma_b^{(* )}$ band.
The overlap functions are calculated numerically
using expressions for $Z^{i}$ integrated over the Dalitz plot.
The diagonal terms, however, can be simplified,
$$
\begin{align}
\rho_0 &= \int_{4m_\pi^2}^{(\sqrt{s}-m_{\Lambda_b})^2}
|f_{0}(\sigma)|^2 \frac{2p_0}{\sqrt{s}} \frac{2q_0}{\sqrt{\sigma}} \, \diff \sigma,\\
\rho_{b^{(* )}} &= \int_{(m_\pi+m_{\Lambda_b})^2}^{(\sqrt{s}-m_\pi)^2}
\frac{1}{(m_{\Sigma_b^{(* )}}^2-\sigma)^2 + m_{\Sigma_b^{(* )}}^2 \Gamma_{\Sigma_b^{(* )}}^2}
\frac{2p_{(* )}}{\sqrt{s}} \frac{R^2p_{(* )}^2}{1+R^2p_{(* )}^2} \frac{2q_{(* )}}{\sqrt{\sigma}}
\frac{R^2q^2}{1+R^2q_{(* )}^2} \, \diff \sigma
\end{align}
$$
for simplicity $f_0(\sigma) = \mathrm{const}$ can be assumed.

```julia; results="hidden"
rhoQTB_Σb(s) = RhoQTB(s,Σb_lineshape,tbs.msq)
rhoQTB_Σb_x(s) = RhoQTB(s,Σb_x_lineshape,tbs.msq)
rhoQTB_f0(s) = RhoQTB(s,(s,σ)->1.0,tbs.msq; channel=2)

# normalization
sN = mΛb2S^2 # arbitrary normalization point
const N0  = rhoQTB_f0(sN)
const N   = rhoQTB_Σb(sN)
const N_x = rhoQTB_Σb_x(sN)
```

```julia; echo=false; fig_cap="Quasi-two-body phase space"
using Plots, LaTeXStrings
pyplot()
#
let eMax = 6.3, len = 100
  ev = range(mΛb+2mπ, eMax; length=len)
  cal0  = rhoQTB_f0.(ev .^ 2)
  cal   = rhoQTB_Σb.(ev .^ 2)
  cal_x = rhoQTB_Σb_x.(ev .^ 2)
  # normalization
  cal0 ./= N0; cal ./= N; cal_x ./= N_x
  # plotting
  plot(size=(500,350), title="Quasi-two-body phase space",
    xlab=L"\sqrt{s}\equiv m_{\Lambda_b\pi\pi}\,(\mathrm{GeV})",
    ylab="QTB phase-space factor")
  plot!(ev, [cal0 cal cal_x],
    lab=[L"f_0\Lambda_b\,S" L"\Sigma_b\pi\,P" L"\Sigma_b^*\pi\,P"])
  vline!([mΣb+mπ], ann=(mΣb+mπ,1.3,text(L"m_{\Sigma_b}+m_\pi",rotation=90,:right,10)), l=(:gray,0.5), lab="")
  vline!([mΣb_x+mπ], ann=(mΣb_x+mπ,1.3,text(L"m_{\Sigma_b}^*+m_\pi",rotation=90,:right,10)), l=(:gray,0.5), lab="")
end
```

## Evaluation of the non-diagonal terms
The phase-space matrix can be calculated exatly using helicity amplitudes described above.
The numerical code below shows the size of the non-diagonal terms for $\sqrt{s}=6.05\,\mathrm{GeV}$,
i.e. at the $\Lambda_b(2S)$ peak.
```julia
using LinearAlgebra
#
Φij = let
    As = [full_Af0, full_AΣb, full_AΣb_x]
    [let
        Aij(σs,two_λs)=Ai(σs,two_λs) * conj(Aj(σs,two_λs))
        I = summed_over_polarization(Aij, tbs)
        three_body_phase_space_integral(I, tbs)
        end for (Ai,Aj) in Iterators.product(As,As)]
end

# evaluation of non-diagonal elements with respect to diagonal
sqrt_diag_Φ = sqrt.(diag(Φij))
Φij_n = Φij ./ sqrt_diag_Φ ./ sqrt_diag_Φ' # set diagonal element to 1.0
display(round.(Φij_n, digits=3)) # make the output
```
One finds the interference of $\Sigma_b$ channel with $\Sigma_b^{* }$ channel very small, $\sim 1.4\,$%,
as expected since the lines do not overlap. The interference of these two channels
with the $f_0$ channel is order of $10\,$%.

## Evaluation of $y$ corrections
The values of $y$ are also evaluated at the peak of $\Lambda_b(2S)$.
It is done by comparing the values of full phase space matrix with the simplified
expresssions, $\rho_{0,b^{(* )}}$ defined above.
```julia
Φ0_Σb   = rhoQTB_Σb(  tbs.msq[4]) * QTB_mismatch_factor(dc_Σb1) * 2
Φ0_Σb_x = rhoQTB_Σb_x(tbs.msq[4]) * QTB_mismatch_factor(dc_Σb_x1) * 2
println("y = ",  round(real(100*(Φij[2,2] - Φ0_Σb) / Φ0_Σb), digits=2), "%")
println("y* = ", round(real(100*(Φij[3,3] - Φ0_Σb_x) / Φ0_Σb_x), digits=2), "%")
```
The cross-channel bands of $\Sigma_b^{(* )}$ do not overlap with each other.
It makes the $y^{(* )}$ value extremely small and justify the approximation.

# Model for the scattering and production amplitude

Given the unitarity constraint, the scattering amplitude is built using the $K$-matrix construction,
$$
\begin{equation}
\hat{T} = (1-i K \Phi)^{-1} K, \qquad
K_{c'c} = \frac{g_{c'} g_{c}}{m^2-s}
\end{equation}
$$
with $c$, $c'$ are channel indices.

An expression for the decay amplitude $\Lambda_b(2S)\to \Lambda_b\pi\pi$
can be constructed using $P$-vector approach.
$$
\begin{equation}
\hat{F} = (1-i K \Phi)^{-1} P, \qquad
P_{c} = \frac{\alpha g_{c}}{m^2-s},
\end{equation}
$$
with $\alpha$ being a normalization constant.

$$
\textit{(a part of derivation is dropped)}
$$

An expression for the differential $\Lambda_b$ cross section is calculated
by as a squared decay amplitude integrated over the three-body phase space
$$
\begin{equation}
\frac{\diff \sigma^{\mathrm{INCL}}}{\diff s} = \frac{\diff \sigma}{\diff s} = \hat{F}^\dagger \, \Phi \, \hat{F},
\end{equation}
$$

Under assumption that $x$, $y$, and $z$ are small numbers,
the expressions simlifies to
$$
\begin{equation}
\frac{\diff \sigma^{\mathrm{INCL}}}{\diff s} =
\alpha^2
\frac{g_0^2\rho_0+g^2\rho+g_*^2\rho_*}
{(m^2-s)^2+(g_0^2\rho_0+g^2\rho+g_*^2\rho_*)^2}
\end{equation}
$$

```julia; results="hidden"
function prodA(s;m=error("give the mass"),
                 g=error("give vector of couplings (g0 g g*)"))
  cal0  = rhoQTB_f0(s)
  cal   = rhoQTB_Σb(s)
  cal_x = rhoQTB_Σb_x(s)
  ρ = [cal0/N0, cal/N, cal_x/N_x]
  return ρ' * g / ((m^2-s)^2 + (ρ' * g.^2)^2)
end
```
```julia; echo=false; fig_cap="Calculated resonance lineshape"
# plotting
let eMax = 6.3, len = 100
  ev = range(mΛb+2mπ, eMax; length=len)
  # plotting
  plot(size=(500,350), title=L"$\Lambda(2S)\,\,\mathrm{lineshape}$",
    xlab=L"\sqrt{s}\equiv m_{\Lambda_b\pi\pi}\,(\mathrm{GeV})",
    ylab=L"$\mathrm{d}\sigma / \mathrm{d} s$", leg=:topleft)
  #
  cal = prodA.(ev .^ 2; m=6.06, g=[0.6, 0.2, 0.2] .* 0.9)
  plot!(ev, cal, lab=L"g=[0.6,0.2,0.2]")
  cal = prodA.(ev .^ 2; m=6.06, g=[0.0, 0.5, 0.5] .* 0.82)
  plot!(ev, cal, lab=L"g=[0.0,0.5,0.5]")
  #
  vline!([mΣb+mπ], ann=(mΣb+mπ,2,text(L"m_{\Sigma_b}+m_\pi",rotation=90,:right,10)), l=(:gray,0.5), lab="")
  vline!([mΣb_x+mπ], ann=(mΣb_x+mπ,2,text(L"m_{\Sigma_b}^*+m_\pi",rotation=90,:right,10)), l=(:gray,0.5), lab="")
end
```
