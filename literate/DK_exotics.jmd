---
title: Investigation of the $D^+K^-$ exotics
author: Misha Mikhasenko
date: 20th November 2019
options:
    dpi : 200
    fig_env : "figure"
    fig_ext : ".pdf"
    out_width : "\\textwidth"
---

```julia; results="hidden"; echo=false
import PyCall
import PyPlot: matplotlib
PyCall.PyDict(matplotlib["rcParams"])["font.family"] = "serif:bold"
# PyCall.PyDict(matplotlib["rcParams"])["font.weight"] = "bold"
PyCall.PyDict(matplotlib["rcParams"])["font.serif"] = "Helvetica" #"STIX"
PyCall.PyDict(matplotlib["rcParams"])["mathtext.fontset"] = "dejavusans"#"Helvetica"
PyCall.PyDict(matplotlib["rcParams"])["text.usetex"] = true
```

# Introduction
There seem to be a structure at $D^+K^-$ channel showing up in the $\bar{B}^0\to \bar{D}^0 D^+K^-$ decay.
The peak position is $2.9\,$GeV, the width is approximately $60\,$MeV.

The following properties of the state can be inferred from the decay mode:
 - The quark content is $D^+(c\bar{d}) + K^-(s\bar{u})$ corresponds to the flavor-exotic state
 - The state has natural quantum numbers: $0^+$, or $1^-$, or $2^+$.

For the calculations below, the masses of particles are set as follows.
```julia; result=hidden
const mB = 5.27932
const mK = 0.493677; const mπ = 0.14;
const mD  = 1.86965; const mD0 = 1.86483
# paramaters of the new state for the reference
const mX =  2.9; const ΓX =0.061
```

# Nearby thresholds

The relecant threshold can be searched in the combination of $D^+$ and $K^-$
excitations as well as $D^0$ and $\bar{K}^0$ due to the quark content.
Two-baryon thresholds, e.g. $\Xi_c+\bar{p}$ are at higher mass.


```julia; results=hidden; fig_cap="Thresholds for the DK system. Exitations of the D are listed along y axis, the colors of the marker indecate the used K state."
mDs = (D=mD, Dstar=2.01026, D1=2.4232+25e-3im, D2star=2.46+46.7e-3im) # with Γ < 50 MeV
mKs = (K=mK, Kstar=0.89166+46e-3im)#
```
```julia; echo=false
using Plots; pyplot()
let cs = [:red, :blue] # colors
    plot(size=(800,300))
    for (e,D) in enumerate(mDs), (i,K) in enumerate(mKs)
        scatter!([real(D+K)], [e], m=(8,cs[i]), lab=(i==1&&e==1) ? "K" : ((i==2&&e==1) ? "K*" : ""))
    end
    annotate!([(mD+mK-0.35,e,text(D, 7, :left)) for (e,D) in enumerate(keys(mDs))])
    plot!(xlim = (mD+mK-0.4, mB-mD0), ylim = (0, length(mDs)+1),
        grid=:y, xlab="m(DK) (GeV)", title = "D+K thresholds", yaxis=false)
    vline!([mX], lab="")
end
```

Three combinations to be considered:
 - The $D^*K^*$ threshold is $2\,$MeV above the studied peak. The $S$-wave molecules of $1^- \otimes 1^-$
    can decay to $DK$, i.e. $0^- \otimes 0^-$ in $S$-wave and $D$-wave, which corresponds to $0^+$ and $2^+$.
 - The $D_1K$ threshold is $17\,$MeV above the studied peak, $1^+ \otimes 0^- = 1^-$, i.e. $DK$ $P$-wave.
 - The $D_2K$ threshold is $54\,$MeV above the peak. The S-wave quantum numbers would $2^{-}$ which does not couple to $DK$ waves (natural parity).

# Lineshape of the $D_1 K$ molecule

A simplest model for the threshold enhancement is an amplitude in the scattering-length approximation
(see the new PDG review on resonances).
A two-channel ampltude reads
$$
\begin{align}
A_{11}(s) &= \frac{1}{c_{11}-i\rho_1(s)-c_{12}^2/(c_{22}-i\rho_2(s))} \xrightarrow[c_{22}=0]{} \frac{1}{c_{11}-i\rho_1(s)-i c_{12}^2/\rho_2(s)},\\
A_{12}(s) &= \frac{-c_{12}}{(c_{11}-i\rho_1(s))(c_{22}-i\rho_2(s))-c_{12}^2} \xrightarrow[c_{22}=0]{} \frac{-ic_{12}/\rho_2(s)}{c_{11}-i\rho_1(s)-i c_{12}^2/\rho_2(s)},
\end{align}
$$
where $\rho_1(s)$ and $\rho_2(s)$ are the phase-space factors for the $D_1 K$ and $D K$ channels, respectively.
For the stable particle, the factors read,
$$
\rho(s) = \sqrt{(s-(m_1+m_2)^2)(s-(m_1-m_2)^2)}/s.
$$
For particles with non-negligible width, its decay needs to be taken into account.
A rough approximation to the effective multi-body phase-space expression is obtained from
the stable phase-space by shifting the masses to the complex value, $m_{D_1}\to m_{D_1}-i\Gamma_{D_1}/2$.

Here is julia code that define functions and calculate the amplitude and intensity
for $c_{11} = -0.07$, $c_{12} = 0.03$:
```julia
ρ(s,m1,m2) = sqrt((s-(m1+m2)^2)*(s-(m1-m2)^2))/s
iρ(s,m1,m2) = 1im*ρ(s,m1,m2)#-sqrt((m1+m2)^2-s)*sqrt(s-(m1-m2)^2)/s
# D1 and K
iρ1(s) = iρ(s,real(mDs.D1)-1im*imag(mDs.D1)/2,real(mKs.K)) # D1+K
iρ2(s) = iρ(s,real(mDs.D),real(mKs.K))  # D+K
A(s,c11,c12,c22) = inv([c11-iρ1(s) c12; c12 c22-iρ2(s)])
A11(s,c11,c12,c22=0) = A(s,c11,c12,c22)[1,1]
A12(s,c11,c12,c22=0) = A(s,c11,c12,c22)[1,2]
A22(s,c11,c12,c22=0) = A(s,c11,c12,c22)[2,2]
# calculation of the model and plotting
let c11 = -0.07, c12 = 0.03, c22=0.0 # suppose to be fit parameters
    ev = range(2.6, 3.2, length=300); sv = ev.^2
    calv = A12.(sv.+1e-6im, c11, c12, c22)
    intv = [abs2(A12(s+1e-6im, c11, c12, c22))*ρ(s,mK,mD)*ρ(s,mD0,mB)/sqrt(s) for s in sv]
    #
    plot(layout=grid(1,2), color_palette=palette(:wong2), xlab="m(DK) (GeV)")
    plot!(sp=1, ev, [real.(calv) imag.(calv)], lab=["Re A" "Im A"], title="amplitude")
    plot!(sp=2, ev, intv, title="intensity", lab="")
    #
    [vline!(sp=sp, [mX], lab="mX", ls=:dash) for sp=1:2]
    [vline!(sp=sp, [real(mDs.D1+mKs.K)], lab="m(D1)+m(K)") for sp=1:2]
    plot!(size=(800,300))
end
```

The Argand-like diagram for the amplitude shows a clear circle, rotates as whole by $90\,$deg.
```julia; echo=false
let c11 = -0.07, c12 = 0.03, c22=0.0 # suppose to be fit parameters
    ev = range(2.6, 3.2, length=300); sv = ev.^2
    calv = A12.(sv .+ 1e-6im, c11, c12, c22)
    #
    plot(size=(800,300), layout=grid(1,2))
    plot!(sp=2, xaxis=false, yaxis=false, grid=false)
    #
    plot!(sp=1,real.(calv), imag.(calv), lab="", title="amplitude A12")
    scatter!(sp=1,real.([calv[1] calv[end]]), imag.([calv[1] calv[end]]),
        m=[:red :blue], ms = 7, lab=["m(DK) = $(ev[1]) GeV" "m(DK) = $(ev[end]) GeV"],
        xlab="Re A12", ylab="Im A12")
end
```

## Accurate estimation of the finite-width effects

Instead of making the mass of $D_1$ complex in the expression for the two-body phase space,
a decay chain $D_1\to D^* \pi$ $S$-wave can be accounted for.
The expression for the effective $D_1 K$ phase-space follows from integrating $D_1\to D^* \pi$ decay degrees of freedom over.
$$
\begin{align}
\rho_{\text{QTB},1}(s) = \frac{1}{n}\int_{(m_{D^* } +m_\pi)^2}^{(\sqrt{s}-m_K)^2} \frac{\mathrm{d}\sigma}{\pi}
    \frac{\rho(\sigma,m_{D^* }^2,m_\pi^2)}{(m_{D1}^2-σ)^2+m_{D1}^2\Gamma_{D1}^2}
    \rho(s,\sigma,m_{K}^2).
\end{align}
$$
Left-hand singularities of the function $\rho_{\text{QTB},1}(s)$ needs to be
removed. It is done using the dispersion integral,
$$
\begin{align}
\text{CM}_{\text{QTB},1}(s) = v_0 + \frac{s}{\pi}\int_{s_\text{th}}^{\infty} \frac{\rho_{\text{QTB},1}(s')}{s'(s'-s-i0)} \mathrm{d}s',
\end{align}
$$
with $s_\text{th} = (m_{D^* } + m_\pi+m_K)^2$, the subtraction constant $v_0$ is such to make the real part of
the $\text{CM}_{\text{QTB},1}$ vanish at the nominal $D_1 K$ threshold.

```julia; echo=false; fig_cap="Comparison of the two functions, iRho1(s), and CM-QTB1(s)"
using QuadGK
using ThreeBodyDecay
# quasi-two-body amplitude
Xlineshape(s,σ) = 1/(mDs.D1^2-σ-1im*real(mDs.D1)*imag(mDs.D1))
const N = quadgk(σ->abs2(Xlineshape(0,σ))*ρ(σ,mDs.Dstar,mπ), (mDs.Dstar+mπ)^2, Inf)[1]
ρ1_QTB(s) = RhoQTB(s,Xlineshape,(mK^2,mπ^2,mDs.Dstar^2)) / N

# two-body amplitude for the asymptotic
const e0 = real(mDs.D1)+mK + 5*imag(mDs.D1)
const Nr = ρ1_QTB(e0^2) / ρ(e0^2,real(mDs.D1),mK)
ρ1_TB(s) = ρ(s,real(mDs.D1),mK) * Nr
# Chew-Mandelstam function (correct analytic structure)
CM1(s) = s/π*quadgk(sp->(sp < e0^2 ? ρ1_QTB(sp) : ρ1_TB(sp))/(sp*(sp-s-1e-6im)), (mDs.Dstar+mπ+mK)^2, Inf)[1]
const CM0 = CM1((real(mDs.D1)+mK)^2)
CM1_sub(s) = CM1(s) - real(CM0)
#
let
    plot(size=(800,300), layout=grid(1,2))
    plot!(sp=2, xaxis=false, yaxis=false, grid=false)
    ev = range(2.6, 3.2, length=20); sv = ev.^2
    calv = CM1_sub.(sv)
    plot!(sp=1, ev, [real.(calv) imag.(calv)], lab=["Re CM" "Im CM"],
        xlab="m(DK) (GeV)" , ylab="effective phase space")
    calv = iρ1.(sv)
    plot!(sp=1, ev, [real.(calv) imag.(calv)], lab=["Re iRho" "Im iRho"], ls=:dash)
end
```
The comparison shows that the more accurate calculation would modify the lineshape slightly.
The approximation is consedered to have a minor effect.
<!-- There are more important effects. -->

# Lineshape of the $D^*K^*$ molecule

The threshold of the $D^*K^*$ is $16\,$MeV higher than the peak position.
The width of the state is singnificantly influenced by the $K^* $ width, which is already $50\,$MeV.
```julia
# K* D phase space
iρ3(s) = iρ(s,real(mKs.Kstar)-1im*imag(mKs.Kstar)/2,real(mDs.Dstar)) # D1+K
B(s,c11,c12,c22) = inv([c11-iρ1(s) c12; c12 c22-iρ3(s)])
B12(s,c11,c12,c22=0) = B(s,c11,c12,c22)[1,2]
#
```

```julia; echo=false; fig_cap="The DK spectrum for c11 = -0.05, c12=0.03."
# calculation of the model and plotting
let c11 = -0.05, c12 = 0.03 # suppose to be fit parameters
    ev = range(2.6, 3.2, length=300); sv = ev.^2
    calv = B12.(sv .+ 1e-6im, c11, c12)
    intv = [abs2(B12(s+1e-6im, c11, c12))*ρ(s,mK,mD)*ρ(s,mD0,mB)/sqrt(s) for s in sv]
    #
    plot(layout=grid(1,2), color_palette=palette(:wong2), xlab="m(DK) (GeV)")
    plot!(sp=1, ev, [real.(calv) imag.(calv)], lab=["Re B12" "Im B12"], title="amplitude")
    plot!(sp=2, ev, intv, title="intensity", lab="")
    #
    [vline!(sp=sp, [mX], lab="mX", ls=:dash) for sp=1:2]
    [vline!(sp=sp, [real(mDs.Dstar+mKs.Kstar)], lab="m(D1)+m(K)") for sp=1:2]
    plot!(size=(800,300))
end
```

# Isospin

The quark content of the exotic candidate is $cs\bar{u}\bar{d}$. A combination of the light quarks $\bar{u}\bar{d}$
has projection to the $I=0$ and $I=1$,
$$
\begin{align}
\left|1,0\right\rangle &= -\frac{\bar{d}\bar{u}+\bar{u}\bar{d}}{\sqrt{2}} &
\left|0,0\right\rangle &= -\frac{\bar{d}\bar{u}-\bar{u}\bar{d}}{\sqrt{2}}.
\end{align}
$$
In case the state has isospin $1$, it would appear in $cs\bar{u}\bar{u}$, i.e. in $D^0 K^-$.
This requires to be checked.

# Triangle singularity

We focus on $B^-\to D^- D^+ K^-$ for the first investigation.
- A system of $D^+ K^-$ $(cs\bar{u}\bar{d})$ can be obtained only from $[D^+][K^-]$ of $[D^0][\bar{K}^0]$ rescattering,
where brakets stands on all possible exitations. A system of $D^0 D^-$ does not couple to narrow resonances (only $Z$ states).
The $[D^+]D^-$ is a prominent decay channel of many charmonium states.
For the on-mass-shell rescattering the mass of the charmonium state needs to be in the range
```julia
[real(mDs.D1)+mD, mB-mK] # GeV
```
and as close as possible to the upper limit.
Aiming for $D_1^+ K^-$ threshold, the charmonium states to be tested are:
 - The state $\chi_{c0}(4700)$ with $(m,\Gamma) = (4.704,0.150)\,$GeV
 - The state $\psi(4660)$ with $(m,\Gamma) = (4.643,0.072)\,$GeV
 - The state $\psi(4500)$ with $(m,\Gamma) = (4.506,0.072)\,$GeV
 - The state $Z_c(4430)$
 - The state $\psi(4415)$ with $(m,\Gamma) = (4.421,0.062)\,$GeV
Stopped here.

### Quantum numbers

# Literature on the molecule binding

There are a few papers discussion $cs\bar{q}\bar{q}$ and $c\bar{s}q\bar{q}$ states,
however more in prospective of the tetraquark, and below the open flavor threshold.
 - Implications of a $DK$ molecule at $2.32\,$GeV by Barnes et al. [(arxiv)](https://arxiv.org/pdf/hep-ph/0305025.pdf), see references therein.
 - "Stable charm and strangeness by Isgur-Lipkin [(inspire)](http://inspirehep.net/record/9830)
 - Quark model calculations [(arxiv)](https://arxiv.org/pdf/hep-ph/0104208.pdf)
So far, I found nothing on $D^* K^* $, neither on $D_1 K$.

 More references:
 - BaBar analysis of $B^0\to D^-D^0 K^+$ [(arxiv)](https://arxiv.org/pdf/1412.6751.pdf)
 - Last Dan's ANA note [(twiki)](https://twiki.cern.ch/twiki/pub/LHCbPhysics/B2DDKAmplitudeAnalysis/ANA_v3.1.pdf)
